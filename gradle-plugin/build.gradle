plugins {
    id 'com.jfrog.bintray'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

repositories {
    google()
    jcenter()
}

sourceCompatibility = "${target}"
targetCompatibility = "${target}"

sourceSets.main {
    java {
        srcDirs = ['src']
        include '**/*.java'
        exclude 'META-INF/**'
    }

    groovy {
        srcDirs = ['src']
        include '**/*.groovy'
        exclude 'META-INF/**'
    }

    resources {
        srcDirs = ['src']
        include '**/*.properties'
        include '**/*.gif'
        include '**/*.png'
        include '**/*.pro'
    }
}

sourceSets.test {
    java {
        srcDirs = ['test']
    }
    groovy {
        srcDirs = ['test']
    }
    resources {
        srcDirs += "$buildDir/fixtures"
    }
}

// Copies selected directories from examples into the test resources to compile during an integration test.
tasks.register('copyExamples', Copy) {
    from("$rootProject.rootDir/examples") {
        include 'spring-boot/**'
        include 'gradle-kotlin-dsl/**'
    }
    into "$buildDir/fixtures"
}

tasks.withType(Test).configureEach {
    dependsOn copyExamples
}

jar {
    metaInf.from 'src/META-INF'
}

dependencies {
    api project(':base')
    compileOnly 'com.android.tools.build:gradle:3.0.0'
    compileOnly gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
    testImplementation "commons-io:commons-io:2.8.0"
}

afterEvaluate {
    /*  This project gets two publications:
        - "pluginMaven" is the default from `java-gradle-plugin`
          and must not be automatically published.
        - "<project.name>" is created by ProGuard custom logic
          and should have its artifact ID adjusted.
     */
    tasks.withType(AbstractPublishToMaven).matching { task ->
        task.publication.name == 'pluginMaven'
    } each {task ->
        task.enabled = false
    }
}
