import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceCompatibility = "${target}"
targetCompatibility = "${target}"

sourceSets.main {
    java {
        srcDirs = ['src']
    }
    resources {
        srcDirs = ['src']
        include '**/*.properties'
        include '**/*.gif'
        include '**/*.png'
        include '**/*.pro'
    }
}

dependencies {
    compile project(':base')
    compile 'com.guardsquare:proguard-core'
    compile project(':retrace')
}

task fatJar(type: ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguardgui.jar')
    from sourceSets.main.output
    configurations = [project.configurations.runtime]
    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Main-Class': 'proguard.gui.ProGuardGUI',
                'Implementation-Version': archiveVersion.get())
    }
}

assemble.dependsOn fatJar
