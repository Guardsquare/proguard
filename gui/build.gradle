import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.gradleup.shadow'
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':base')
    implementation project(':retrace')
    implementation 'org.apache.logging.log4j:log4j-api:2.24.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.2'
}

tasks.named('shadowJar', ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguardgui.jar')
    manifest {
        attributes(
                'Main-Class': 'proguard.gui.ProGuardGUI',
                'Multi-Release': true,
                'Implementation-Version': archiveVersion.get(),
        )
    }
}

afterEvaluate {
    publishing {
        publications.getByName(project.name) {
            pom {
                description = 'ProGuardGUI is an interface for ProGuard, the free shrinker, optimizer, obfuscator, and preverifier for Java bytecode'
            }
        }
    }
}
