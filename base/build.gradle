plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'de.jansauer.printcoverage' version '2.0.0'
    id 'jacoco'
    id "org.jlleitschuh.gradle.ktlint" version '10.2.1'
}

repositories {
    mavenCentral()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
  kotlinOptions {
    jvmTarget = "${target}"
  }
}

dependencies {
    api "com.guardsquare:proguard-core:${proguardCoreVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.json:json:20220924'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation 'dev.zacsweers.kctfork:core:0.2.1'
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.5.4' // for kotest framework
    testImplementation 'io.kotest:kotest-assertions-core-jvm:5.5.4' // for kotest core jvm assertions
    testImplementation 'io.kotest:kotest-property-jvm:5.5.4' // for kotest property test
    testImplementation 'io.mockk:mockk:1.13.2' // for mocking

    testImplementation(testFixtures("com.guardsquare:proguard-core:9.0.8")) {
        exclude group: 'com.guardsquare', module: 'proguard-core'
    }
}

jar {
    manifest {
        attributes(
                'Multi-Release': true,
                'Implementation-Version': archiveVersion.get())
    }
}

// Early access automatic downloads are not yet supported:
// https://github.com/gradle/gradle/issues/14814
// But it will work if e.g. Java N-ea is pre-installed
def javaVersionsForTest = 9..21

test {
    useJUnitPlatform()
}

task testAllJavaVersions() { testAllTask ->
    dependsOn(test) // the usual test runs on Java 8

    javaVersionsForTest.each {version ->
        task("testJava$version", type: Test) {
            useJUnitPlatform()
            ignoreFailures = true

            // The version of bytebuddy used by mockk only supports Java 20 experimentally so far
            if (version >= 20) systemProperty 'net.bytebuddy.experimental', true

            testAllTask.dependsOn(it)

            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(version)
            }
        }
    }
}

jacocoTestReport {
    // Define which classes need to be monitored
    def sources = files(project.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom(sources)
    additionalSourceDirs.setFrom(sources)
    sourceDirectories.setFrom(sources)
    def classes = files(project.sourceSets.main.output.classesDirs)
    classDirectories.setFrom(classes)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/*.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

afterEvaluate {
    publishing {
        publications.getByName(project.name) {
            pom {
                description = 'ProGuard is a free shrinker, optimizer, obfuscator, and preverifier for Java bytecode'
            }
        }
    }
}
