import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceCompatibility = "${target}"
targetCompatibility = "${target}"

sourceSets.main {
    java {
        srcDirs = ['src']
    }
    resources {
        srcDirs = ['src']
        include '**/*.properties'
        include '**/*.gif'
        include '**/*.png'
        include '**/*.pro'
    }
}

dependencies {
    compile 'com.guardsquare:proguard-core:7.0.0'
    compile "com.google.code.gson:gson:${gsonVersion}"
}

task fatJar(type: ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguard.jar')
    from sourceSets.main.output
    configurations = [project.configurations.runtime]
    manifest {
        attributes(
                'Main-Class': 'proguard.ProGuard',
                'Implementation-Version': archiveVersion.get())
    }
}

assemble.dependsOn fatJar
