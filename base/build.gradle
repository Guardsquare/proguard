import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'com.jfrog.bintray'
    id 'java'
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceCompatibility = "${target}"
targetCompatibility = "${target}"

sourceSets.main {
    java {
        srcDirs = ['src']
    }
    resources {
        srcDirs = ['src']
        include '**/*.properties'
        include '**/*.gif'
        include '**/*.png'
        include '**/*.pro'
    }
}

dependencies {
    implementation "com.guardsquare:proguard-core:${proguardCoreVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
}

jar.manifest.attributes('Implementation-Version': version)

task fatJar(type: ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguard.jar')
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    manifest {
        attributes(
                'Main-Class': 'proguard.ProGuard',
                'Implementation-Version': archiveVersion.get())
    }
}

assemble.dependsOn fatJar
