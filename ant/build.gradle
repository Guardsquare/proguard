import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':base')
    implementation 'org.apache.ant:ant:1.10.15'
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.11.1")
    testImplementation("org.mockito:mockito-core:4.11.0")
    testImplementation("org.mockito:mockito-junit-jupiter:4.11.0")
    compileOnly("com.diffblue.cover:cover-annotations:1.7.0")
    testImplementation("com.diffblue.cover:cover-annotations:1.7.0")
}

test {
    useJUnitPlatform()
}

task fatJar(type: ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguard-ant.jar')
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Multi-Release': true,
                'Implementation-Version': archiveVersion.get())
    }
}

assemble.dependsOn fatJar

afterEvaluate {
    publishing {
        publications.getByName(project.name) {
            pom {
                description = 'Ant plugin for ProGuard, the free shrinker, optimizer, obfuscator, and preverifier for Java bytecode'
            }
        }
    }
}
