import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.gradleup.shadow'
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':base')
    implementation 'org.apache.ant:ant:1.10.15'
}

tasks.named('shadowJar', ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguard-ant.jar')
    manifest {
        attributes(
                'Multi-Release': true,
                'Implementation-Version': archiveVersion.get(),
        )
    }
}

afterEvaluate {
    publishing {
        publications.getByName(project.name) {
            pom {
                description = 'Ant plugin for ProGuard, the free shrinker, optimizer, obfuscator, and preverifier for Java bytecode'
            }
        }
    }
}
